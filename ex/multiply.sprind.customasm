#include "../sprind.customasm"
#bank rom

#align SPRIND_SUBR_ALIGN
;= subr(operand1 => x, operand2 => y) -> (u32 in MP) {
multiply:
      pushw a
      pushw b

      ;= alias!(Q => a:b)

      ; Q <- zxt(x)
      mov Q.lo, x
      clr Q.hi

      ; MP <- 0
      wr.mp.lo Q.hi
      wr.mp.hi Q.hi

      ;= replicate!(16) {
            ;= alias!(mask => z)

            ; Copy `y` into `mask`
            mov mask, y
            andi mask, 1
            subi mask, 1
            inv mask

            ; CY <- 0
            clr.cy

            ;= alias!(m_tmp_masked => x)
            ;= alias!(mp_tmp => w)

            ; MP.lo <- MP.lo + (Q.lo & mask)
            mov m_tmp_masked, mask
            and m_tmp_masked, Q.lo
            rd.mp.lo mp_tmp
            addcy mp_tmp, m_tmp_masked
            wr.mp.lo mp_tmp

            ; MP.hi <- MP.hi + (Q.hi & mask) + CY
            mov m_tmp_masked, mask
            and m_tmp_masked, Q.hi
            rd.mp.hi mp_tmp
            addcy mp_tmp, m_tmp_masked
            wr.mp.hi mp_tmp

            ; Q <- Q << 1
            clr.cy
            lslcy Q.lo, 1
            lslcy Q.hi, 1

            ; y <- y >> 1
            lsr y, 1
      ;= }

      popw b
      popw a
      ret
;= }